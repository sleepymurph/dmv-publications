set xlabel "File Size (linear scale)"
set xrange [1*2**20:1.05*2**30]
set format x "%1.0b%BB"
set xtics 128*2**20

set ylabel "Time to commit"

ticstring = "("
do for [s=0:1200:10] {
    ticstring = ticstring.'"'.s.'s" '.s.', '
}
ticstring = ticstring.')'
set ytics @ticstring

set grid
set key top left

# < grep "False"  selects only the lines with no errors

plot \
    '< ../data/meantable.py --columns filebytes c1_time --files exp--file-size--v04--w-cpu/git-*' \
        using 1:2:3 with errorlines title "git", \
    '< ../data/meantable.py --columns filebytes c1_time --files exp--file-size--v04--w-cpu/hg-*' \
        using 1:2:3 with errorlines title "hg", \
    '< ../data/meantable.py --columns filebytes c1_time --files exp--file-size--v04--w-cpu/bup-*' \
        using 1:2:3 with errorlines title "bup", \
    '< ../data/meantable.py --columns filebytes c1_time --files exp--file-size--v04--w-cpu/prototype-*' \
        using 1:2:3 with errorlines title "prototype", \
    '< ../data/meantable.py --columns filebytes c1_time --files exp--file-size--v04--w-cpu/copy-*' \
        using 1:2:3 with errorlines title "cp", \
