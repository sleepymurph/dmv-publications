set xlabel "File Size (linear scale)"
set xrange [512*2**20:8.5*2**30]
set format x "%1.0b%BB"
set xtics 1*2**30

set ylabel "Time to commit"

ticstring = "("
do for [m=0:1200:10] {
    ticstring = ticstring.'"'.m.'m" '.m.'*60, '
}
ticstring = ticstring.')'
set ytics @ticstring

set grid
set key top left

meantable(f)="< ../data/meantable.py --columns filebytes c1_time --files ../data/exp--file-size--v04--w-cpu/".f

plot \
    meantable('git-*') using 1:2:3 with errorlines title "git", \
    meantable('hg-*') using 1:2:3 with errorlines title "hg", \
    meantable('bup-*') using 1:2:3 with errorlines title "bup", \
    meantable('prototype-*') using 1:2:3 with errorlines title "prototype", \
    meantable('copy-*') using 1:2:3 with errorlines title "cp", \
