set title "Time to commit increasingly numerous files (1KiB each)"

set xlabel "File Count"
set logscale x 2
set format x "%1.0b%B"

set ylabel "Time to add and commit"
set logscale y 60
set ytics ( \
    "1ms" .001, "10ms" .01, "20ms" .02, "50ms" .05, \
    "100ms" .1, "200ms" .2, "500ms" .5, \
    "1s" 1, "2s" 2, "5s" 5, "10s" 10, "30s" 30, \
    "1m" 60, "2m" 2*60, "5m" 5*60, "10m" 10*60, "30m" 30*60, \
    "1h" 60*60, "2h" 2*60*60, "5h" 5*60*60, "10h" 10*60*60)

set grid
set key top left

# set xrange [2**17 : 2**22]
# set yrange [60 : 60*60]

# < grep "False"  selects only the lines with no errors

plot \
    '< grep "False" exp--increasing-number-of-files--v01/test-2016-10-06--increasing-file-count--git.txt' \
        using 2:8 with linespoints title "git (initial commit)", \
    \
    '< grep "False" exp--increasing-number-of-files--v01/test-2016-10-06--increasing-file-count--hg.txt' \
        using 2:8 with linespoints title "mercurial (initial commit)", \
    \
    '< grep "False" exp--increasing-number-of-files--v01/test-2016-10-06--increasing-file-count--git.txt' \
        using 2:13 with linespoints title "git (commit updated 1/16 files)", \
    \
    '< grep "False" exp--increasing-number-of-files--v01/test-2016-10-06--increasing-file-count--hg.txt' \
        using 2:13 with linespoints title "mercurial (commit updated 1/16 files)", \
