# Graph output of the increasing file size test
#
# To run:
#   gnuplot -p increasing-file-size--time-comparison.gpi
#   gnuplot -e "set terminal pdf" increasing-file-size--time-comparison.gpi > output.pdf
#

set title "Time to commit files of increasing size to version control systems"

set xlabel "File Size"
set logscale x 2
set format x "%1.0b%B"

set ylabel "Time to add and commit"
set logscale y 60
set ytics ( \
    "1ms" .001, "10ms" .01, "20ms" .02, "50ms" .05, \
    "100ms" .1, "200ms" .2, "500ms" .5, \
    "1s" 1, "2s" 2, "5s" 5, "10s" 10, "30s" 30, \
    "1m" 60, "2m" 2*60, "5m" 5*60, "10m" 10*60, "20m" 20*60, \
    "1h" 60*60, "2h" 2*60*60, "5h" 5*60*60, "10h" 10*60*60)

set grid
set key top left

# < grep "False"  selects only the lines with no errors

plot \
    '< grep "False" test-2016-10-03--small-change--git--mmurphytest01.txt' using 2:5 with linespoints title "git (initial)", \
    '< grep "False" test-2016-10-03--small-change--git--mmurphytest01.txt' using 2:8 with linespoints title "git (update)", \
    '< grep "False" test-2016-10-04--small-change--hg--mmurphytest01.txt' using 2:5 with linespoints title "mercurial (initial)", \
    '< grep "False" test-2016-10-04--small-change--hg--mmurphytest01.txt' using 2:8 with linespoints title "mercurial (update)"
