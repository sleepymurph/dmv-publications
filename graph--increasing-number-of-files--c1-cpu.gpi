set xlabel "Number of files (log scale)"
set logscale x 10
set format x "%1.0s%c"

set ylabel "CPU Usage"
set yrange [0:100]
set format y "%1.0f%%"

set grid
set key top left

set multiplot layout 2,2 rowsfirst
meantable="< ./meantable.py --columns filecount c1_user c1_system c1_idle c1_iowait --files exp--increasing-number-of-files--v02--w-cpu"
POS="right at graph 0.95,0.88 front"
USERPLOT='using 1:(($2+$4+$8)/($2+$4+$6+$8)*100) with filledcurves y1=0 title "user"'
SYSTEMPLOT='using 1:(($4+$8)/($2+$4+$6+$8)*100) with filledcurves y1=0 title "system"'
IOWAITPLOT='using 1:(($8)/($2+$4+$6+$8)*100) with filledcurves y1=0 title "iowait"'

set label 1 'git' @POS
set ylabel "Git CPU Usage"
gittable=meantable."/git-*"
plot gittable @USERPLOT, gittable @SYSTEMPLOT, gittable @IOWAITPLOT

set label 1 'hg' @POS
set ylabel "Hg CPU Usage"
hgtable=meantable."/hg-*"
plot hgtable @USERPLOT, hgtable @SYSTEMPLOT, hgtable @IOWAITPLOT

set label 1 'bup' @POS
set ylabel "Bup CPU Usage"
buptable=meantable."/bup-*"
plot buptable @USERPLOT, buptable @SYSTEMPLOT, buptable @IOWAITPLOT

set label 1 'copy' @POS
set ylabel "Copy CPU Usage"
copytable=meantable."/copy-*"
plot copytable @USERPLOT, copytable @SYSTEMPLOT, copytable @IOWAITPLOT
