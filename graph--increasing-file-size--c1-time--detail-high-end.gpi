set xlabel "File Size (linear scale)"
set xrange [512*2**20:65*2**30]
set format x "%1.0b%BB"
set xtics 8*2**30

set ylabel "Time to commit"

minutetics = 10
ticstring = "("
do for [m=minutetics:59:minutetics] {
    ticstring = ticstring.'"'.m.'m" '.m.'*60, '
}
do for [h=1:4] {
    ticstring = ticstring.'"'.h.'h" '.h.'*60*60, '
    do for [m=minutetics:59:minutetics] {
        if (m<10) {
            ticstring = ticstring.'"'.h.':0'.m.'h" ('.h.'*60+'.m.')*60, '
        } else {
            ticstring = ticstring.'"'.h.':'.m.'h" ('.h.'*60+'.m.')*60, '
        }
    }
}
ticstring = ticstring.')'
set ytics @ticstring

set grid
set key top left

# < grep "False"  selects only the lines with no errors

plot \
    '< ./meantable.py --columns filebytes c1_time --files exp--increasing-file-size--v04--w-cpu/git-*' \
        using 1:2:3 with errorlines title "git", \
    '< ./meantable.py --columns filebytes c1_time --files exp--increasing-file-size--v04--w-cpu/hg-*' \
        using 1:2:3 with errorlines title "hg", \
    '< ./meantable.py --columns filebytes c1_time --files exp--increasing-file-size--v04--w-cpu/bup-*' \
        using 1:2:3 with errorlines title "bup", \
    '< ./meantable.py --columns filebytes c1_time --files exp--increasing-file-size--v04--w-cpu/copy-*' \
        using 1:2:3 with errorlines title "cp", \
