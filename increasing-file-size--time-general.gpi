# Graph output of the increasing file size test
#
# To run:
#   gnuplot -p increasing-file-size.gpi < input_data.dat
#   gnuplot -e "set terminal pdf" increasing-file-size.gpi < input_data.dat  > output.pdf
#

set title "Time to add files to Git"

set xlabel "File Size"
set logscale x 2
set format x "%1.0b%B"

set ylabel "Time to add and commit"
set logscale y 60
set ytics ( \
    "1ms" .001, "10ms" .01, "20ms" .02, "50ms" .05, \
    "100ms" .1, "200ms" .2, "500ms" .5, \
    "1s" 1, "2s" 2, "5s" 5, "10s" 10, "30s" 30, \
    "1m" 60, "2m" 2*60, "5m" 5*60, "10m" 10*60, "20m" 20*60, \
    "1h" 60*60)

set grid

plot '/dev/stdin' using 2:5 with linespoints title ""
