meantable(f) = "< ../data/meantable.py \
    --columns filebytes c1_time \
    --where-not-bad c1_ver \
    --files ../data/exp--file-size--v04--w-cpu/".f.'-*'

set format x "%1.0b%BB"

set grid
unset key

kilos(n) = n * 2**10
megs(n) = n * 2**20
gigs(n) = n * 2**30

mins(n) = n * 60

set multiplot layout 2,2 rowsfirst

set logscale x 2
set logscale y 60
set xrange [kilos(1):gigs(128)]
set ytics ("10ms" .01, "100ms" .1, "1s" 1, "1m" 60, "1h" mins(60))
set xtics (kilos(1), megs(1), gigs(1), gigs(128))
set yrange [.001:mins(600)]

set title "(a) Logarithmic Scale"
plot \
    meantable("prototype") using 1:2 with lines linetype 10 title "DMV (h=2,n=2)", \
    meantable("prototype2x1mem") using 1:2 with lines linetype 4 title "DMV (h=2,n=1)", \
    meantable("bup") using 1:2 with lines linetype 3 title "Bup"

unset logscale xy


# Separate key: do another plot with outlandish ranges and all decoration off

set xrange [8*2**30:104*2**30]
set yrange [-mins(400):-mins(500)]
unset xtics
unset ytics
unset grid
unset border
set key center center

unset title
plot \
    meantable("prototype") using 1:2:3 with errorlines linetype 10 title "DMV (h=2,n=2)", \
    meantable("prototype2x1mem") using 1:2:3 with errorlines linetype 4 title "DMV (h=2,n=1)", \
    meantable("bup") using 1:2:3 with errorlines linetype 3 title "Bup"


# Reestablish decorations

set grid
set border
unset key
set yrange [*:*]


set mxtics 2
set mytics 2


# Other plots

set xrange [megs(512):gigs(3.5)]
set xtics gigs(1)
set yrange [*:*]
set ytics
set format y "%1.0fm"

set title "(b) Megabytes-scale"
plot \
    meantable("prototype") using 1:($2/60):($3/60) with errorlines linetype 10 title "DMV (h=2,n=2)", \
    meantable("prototype2x1mem") using 1:($2/60):($3/60) with errorlines linetype 4 title "DMV (h=2,n=1)", \
    meantable("bup") using 1:($2/60):($3/60) with errorlines linetype 3 title "Bup"


set xrange [gigs(16):gigs(112)]
set xtics gigs(32)
set yrange [0:360]
set ytics 60
set format y "%1.0fm"

set title "(c) Largest scales measured"
plot \
    meantable("prototype") using 1:($2/60):($3/60) with errorlines linetype 10 title "DMV (h=2,n=2)", \
    meantable("prototype2x1mem") using 1:($2/60):($3/60) with errorlines linetype 4 title "DMV (h=2,n=1)", \
    meantable("bup") using 1:($2/60):($3/60) with errorlines linetype 3 title "Bup"
