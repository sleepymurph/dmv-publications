#set xlabel "Directory Depth"

#set ylabel "File/Dir count"
set format y "%1.0s%c"

set key below
set tmargin 1.5
labelpos = "right at graph 1, 1.08 front"


set style data histograms
set style histogram cluster
set style fill solid

files="../data/exp--filesystem-limits--micro/*murphytest03*"
cmdline(s)="\"< ./plot-filesystem-limits--directory-takeover--preprocess.sh ".s." ".files."\""

datacmd1=cmdline(1)
datacmd2=cmdline(2)
datacmd3=cmdline(3)
datacmd4=cmdline(4)

set multiplot layout 2,2 rowsfirst

set label 1 '1 hex digit per dir' @labelpos
plot \
    @datacmd1 using 4:xticlabels(3) title "files",\
    @datacmd1 using 5:xticlabels(3) title "dirs"

set label 1 '2 hex digits per dir' @labelpos
plot \
    @datacmd2 using 4:xticlabels(3) title "files",\
    @datacmd2 using 5:xticlabels(3) title "dirs"

set label 1 '3 hex digits per dir' @labelpos
plot \
    @datacmd3 using 4:xticlabels(3) title "files",\
    @datacmd3 using 5:xticlabels(3) title "dirs"

set label 1 '4 hex digits per dir' @labelpos
plot \
    @datacmd4 using 4:xticlabels(3) title "files",\
    @datacmd4 using 5:xticlabels(3) title "dirs"
