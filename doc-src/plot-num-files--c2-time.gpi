# set xlabel "Number of files (log scale)"
set format x "%1.0s%c"

#set ylabel "Time to commit"

set grid
unset key

kilos(n) = n * 2**10
megs(n) = n * 2**20
gigs(n) = n * 2**30

mins(n) = n * 60

vcslist = "git hg bup prototype2x1mem copy"

meantable(f)="< ../data/meantable.py \
    --columns filecount c2_time \
    --where-not-bad c2_ver \
    --files ../data/exp--num-files--v03--less-dirs/".f.'-*'

set multiplot layout 3,2 rowsfirst

set logscale x 10
set logscale y 60
set xrange [1:10e6]
set ytics ("10ms" .01, "100ms" .1, "1s" 1, "1m" 60, "1h" mins(60))
set yrange [.001:mins(240)]

plot for [vcs in vcslist] \
    meantable(vcs) using 1:2 with lines title vcs

unset logscale xy


# Separate key: do another plot with outlandish ranges and all decoration off

set xrange [8*2**30:104*2**30]
set yrange [mins(400):mins(500)]
unset xtics
unset ytics
unset grid
unset border
set key center center
plot for [vcs in vcslist] \
    meantable(vcs) using 1:2:3 with errorlines title vcs


# Reestablish decorations

set grid
set border
unset key
set yrange [*:*]


set mxtics 2
set mytics 2


# Other plots

set xrange [50:1050]
set xtics 200
set yrange [0:2]
set ytics 0.5
set format y "%1.1fs"

plot for [vcs in vcslist] \
    meantable(vcs) using 1:2:3 with errorlines title vcs


set xrange [500:10500]
set xtics 2000
set yrange [0:7]
set ytics 1
set format y "%1.1fs"

plot for [vcs in vcslist] \
    meantable(vcs) using 1:2:3 with errorlines title vcs


set xrange [50000:1050000]
set xtics 200000
set yrange [0:20]
set ytics 5
set format y "%1.0fm"

plot for [vcs in vcslist] \
    meantable(vcs) using 1:($2/60):($3/60) with errorlines title vcs


set xrange [50000:10500000]
set xtics 2000000
set yrange [0:360]
set ytics 60
set format y "%1.0fm"

plot for [vcs in vcslist] \
    meantable(vcs) using 1:($2/60):($3/60) with errorlines title vcs
