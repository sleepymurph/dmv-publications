meantable(f)="< ../data/meantable.py \
    --columns filecount c1_time \
    --where-not-bad c1_ver \
    --files ../data/exp--num-files--v03--less-dirs/".f.'-*'

# set xlabel "Number of files (log scale)"
set format x "%1.0s%c"

#set ylabel "Time to commit"

set grid
unset key

kilos(n) = n * 2**10
megs(n) = n * 2**20
gigs(n) = n * 2**30

mins(n) = n * 60

vcslist = "git hg bup prototype2x1mem copy"
vcslabels = "Git Mercurial Bup DMV Copy"

set multiplot layout 2,2 rowsfirst

# Separate key: do another plot with outlandish ranges and all decoration off

set xrange [8*2**30:104*2**30]
set yrange [mins(400):mins(500)]
unset xtics
unset ytics
unset grid
unset border
set key center center
unset title
plot for [vcs in vcslabels] \
    meantable("bup") using 1:2:3 with errorlines title vcs


# Reestablish decorations

set grid
set border
unset key
set yrange [*:*]


set mxtics 2
set mytics 2


# Other plots

set xrange [500:10500]
set xtics 2000
set yrange [0:7]
set ytics 1
set format y "%1.1fs"

set title "Thousands of Files"
plot for [vcs in vcslist] \
    meantable(vcs) using 1:2:3 with errorlines title vcs


set xrange [50000:1050000]
set xtics 200000
set yrange [0:20]
set ytics 5
set format y "%1.0fm"

set title "Hundreds of Thousands of Files"
plot for [vcs in vcslist] \
    meantable(vcs) using 1:($2/60):($3/60) with errorlines title vcs


set xrange [50000:10500000]
set xtics 2000000
set yrange [0:360]
set ytics 60
set format y "%1.0fm"

set title "Millions of Files"
plot for [vcs in vcslist] \
    meantable(vcs) using 1:($2/60):($3/60) with errorlines title vcs
