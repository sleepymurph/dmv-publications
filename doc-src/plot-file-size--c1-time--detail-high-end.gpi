#set xlabel "File Size (linear scale)"
set format x "%1.0b%BB"

#set ylabel "Time to commit"

set grid
unset key

megs(n) = n * 2**20
gigs(n) = n * 2**30

vcslist = "git hg bup prototype2x1mem copy"

meantable(f) = "< ../data/meantable.py" \
    ." --columns filebytes c1_time" \
    ." --files ../data/exp--file-size--v04--w-cpu/".f.'-*'


set multiplot layout 2,2 rowsfirst

set xrange [megs(128):gigs(1)+megs(128)]
set xtics megs(256)

ticstring = "("
do for [s=0:1200:10] {
    ticstring = ticstring.'"'.s.'s" '.s.', '
}
ticstring = ticstring.')'
set ytics @ticstring

plot for [vcs in vcslist] \
    meantable(vcs) using 1:2:3 with errorlines title vcs


set xrange [gigs(2):gigs(18)]
set xtics gigs(4)

ticstring = "("
do for [m=0:330:5] {
    ticstring = ticstring.'"'.m.'m" '.m.'*60, '
}
ticstring = ticstring.')'
set ytics @ticstring

plot for [vcs in vcslist] \
    meantable(vcs) using 1:2:3 with errorlines title vcs


set xrange [gigs(16):gigs(104)]
set xtics gigs(32)

ticstring = "("
do for [m=0:330:60] {
    ticstring = ticstring.'"'.m.'m" '.m.'*60, '
}
ticstring = ticstring.')'
set ytics @ticstring

plot for [vcs in vcslist] \
    meantable(vcs) using 1:2:3 with errorlines title vcs

set xrange [8*2**30:104*2**30]
set yrange [400*60*60:500*60*60]
unset xtics
unset ytics
unset grid
unset border
set key center center
plot for [vcs in vcslist] \
    meantable(vcs) using 1:2:3 with errorlines title vcs
