meantable="< ../data/meantable.py --columns filecount c2_size --files ../data/exp--num-files--v03--less-dirs/"
eachbytes=1024

set multiplot layout 2,2 rowsfirst

set grid
set border
unset key

set xlabel "Number of files (log scale)"
set logscale x 10
set xrange [*:*]
set xtics (10, 1e3, 100e3, 10e6)
set mxtics
set format x "%1.0s%c"

set ylabel "Storage (log scale)"
set logscale y 2
set yrange [2**10:128*2**30]
set ytics
unset mytics
set format y "%1.0b%BB"

set title "(a) Total Disk Space Used"
plot \
    meantable."/git-*" using 1:2:3 with lines title "git (two commits)", \
    meantable."/hg-*" using 1:2:3 with lines title "hg (two commits)", \
    meantable."/bup-*" using 1:2:3 with lines title "bup (two commits)", \
    meantable."/prototype2x1mem-*" using 1:2:3 with lines title "prototype (two commits)", \

unset logscale y


# Separate key: do another plot with outlandish ranges and all decoration off

unset xlabel
unset xtics
unset ylabel
unset ytics
unset grid
unset border
set key center center
set yrange [0:6]

unset title
plot \
    meantable."/git-*" using 1:2:3 with lines title "Git (two commits)", \
    meantable."/hg-*" using 1:2:3 with lines title "Mercurial (two commits)", \
    meantable."/bup-*" using 1:2:3 with lines title "Bup (two commits)", \
    meantable."/prototype2x1mem-*" using 1:2:3 with lines title "DMV (two commits)", \


# Reestablish decorations

set grid
set border
unset key


set xlabel "Number of files (log scale)"
set logscale x 10
set xrange [*:*]
set xtics
set mxtics
set format x "%1.0s%c"

set ylabel "Storage ratio"
set yrange [0:20]
set ytics
set mytics
set format y "%.0fx"

set size 1,0.5
set title "(b) Ratio of Total Disk Space Used to File Size"
plot \
    meantable."/git-*" using 1:($2/$1/eachbytes):($3/$1/eachbytes) with lines title "git (two commits)", \
    meantable."/hg-*" using 1:($2/$1/eachbytes):($3/$1/eachbytes) with lines title "hg (two commits)", \
    meantable."/bup-*" using 1:($2/$1/eachbytes):($3/$1/eachbytes) with lines title "bup (two commits)", \
    meantable."/prototype2x1mem-*" using 1:($2/$1/eachbytes):($3/$1/eachbytes) with lines title "prototype (two commits)", \
