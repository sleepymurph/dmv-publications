# set xlabel "Number of files (log scale)"
set format x "%1.0s%c"

#set ylabel "Time to commit"

set grid
unset key

kilos(n) = n * 2**10
megs(n) = n * 2**20
gigs(n) = n * 2**30

mins(n) = n * 60

meantable(f)="< ../data/meantable.py \
    --columns filecount c1_time \
    --where-not-bad c1_ver \
    --files ../data/exp--num-files--v03--less-dirs/".f.'-*'

set multiplot layout 3,2 rowsfirst

set logscale x 10
set logscale y 60
set xrange [1:10e6]
set ytics ("10ms" .01, "100ms" .1, "1s" 1, "1m" 60, "1h" mins(60))
set yrange [.001:mins(240)]

set title "(a) Logarithmic Scale"
plot \
    meantable("prototype2x1mem") using 1:2 with lines linetype 4 title "cfq", \
    meantable("prototype2x1memdeadline") using 1:2 with lines linetype 7 title "deadline", \
    meantable("prototype2x1memnoop") using 1:2 with lines linetype 8 title "noop"

unset logscale xy


# Separate key: do another plot with outlandish ranges and all decoration off

set xrange [8*2**30:104*2**30]
set yrange [mins(400):mins(500)]
unset xtics
unset ytics
unset grid
unset border
set key center center

unset title
plot \
    meantable("prototype2x1mem") using 1:2:3 with errorlines linetype 4 title "cfq", \
    meantable("prototype2x1memdeadline") using 1:2:3 with errorlines linetype 7 title "deadline", \
    meantable("prototype2x1memnoop") using 1:2:3 with errorlines linetype 8 title "noop"


# Reestablish decorations

set grid
set border
unset key
set yrange [*:*]


set mxtics 2
set mytics 2


# Other plots

set xrange [500000:5500000]
set xtics 1000000
set yrange [0:360]
set ytics 60
set format y "%1.0fm"

set title "(b) Millions of Files"
plot \
    meantable("prototype2x1mem") using 1:($2/60):($3/60) with errorlines linetype 4 title "cfq", \
    meantable("prototype2x1memdeadline") using 1:($2/60):($3/60) with errorlines linetype 7 title "deadline", \
    meantable("prototype2x1memnoop") using 1:($2/60):($3/60) with errorlines linetype 8 title "noop"

set xrange [3750000:5250000]
set xtics 500000
set yrange [210:330]
set ytics 30
set format x "%1.1s%c"
set format y "%1.0fm"

set title "(c) 4-5 Million Files"
plot \
    meantable("prototype2x1mem") using 1:($2/60):($3/60) with errorlines linetype 4 title "cfq", \
    meantable("prototype2x1memdeadline") using 1:($2/60):($3/60) with errorlines linetype 7 title "deadline", \
    meantable("prototype2x1memnoop") using 1:($2/60):($3/60) with errorlines linetype 8 title "noop"
