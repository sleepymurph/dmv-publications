meantable="< ../data/meantable.py --columns filebytes c2_size gc_size --files ../data/exp--file-size--v04--w-cpu"

set multiplot layout 2,2 rowsfirst

set grid
set border
unset key

set xlabel "File Size (log scale)"
set logscale x 2
set xrange [2**10:2**40]
set xtics (1024, 1024**2, 1024**3, 1024**4)
set mxtics
set format x "%1.0b%BB"

set ylabel "Storage (log scale)"
set logscale y 2
set yrange [2**10:2**40]
set ytics
unset mytics
set format y "%1.0b%BB"

plot \
    meantable."/gitallowrepoerr-*" using 1:2:3 with lines title "git (two commits)", \
    meantable."/hg-*" using 1:2:3 with lines title "hg (two commits)", \
    meantable."/bup-*" using 1:2:3 with lines title "bup (two commits)", \
    meantable."/prototype2x1mem-*" using 1:2:3 with lines title "prototype (two commits)", \
    meantable."/gitallowrepoerr-*" using 1:4:5 with lines title "git (garbage collected)" linetype 6, \

unset logscale y


# Separate key: do another plot with outlandish ranges and all decoration off

unset xlabel
unset xtics
unset ylabel
unset ytics
unset grid
unset border
set key center center
set yrange [0:6]
plot \
    meantable."/gitallowrepoerr-*" using 1:2:3 with lines title "git (two commits)", \
    meantable."/hg-*" using 1:2:3 with lines title "hg (two commits)", \
    meantable."/bup-*" using 1:2:3 with lines title "bup (two commits)", \
    meantable."/prototype2x1mem-*" using 1:2:3 with lines title "prototype (two commits)", \
    meantable."/gitallowrepoerr-*" using 1:4:5 with lines title "git (garbage collected)" linetype 6, \


# Reestablish decorations

set grid
set border
unset key


set xlabel "File Size (log scale)"
set logscale x 2
set xrange [2**10:2**40]
set xtics
set mxtics
set format x "%1.0b%BB"

set ylabel "Storage ratio"
set yrange [0:5]
set ytics
set mytics
set format y "%.0fx"

set size 1,0.5
plot \
    meantable."/gitallowrepoerr-*" using 1:($2/$1):($3/$1) with lines title "git (two commits)", \
    meantable."/hg-*" using 1:($2/$1):($3/$1) with lines title "hg (two commits)", \
    meantable."/bup-*" using 1:($2/$1):($3/$1) with lines title "bup (two commits)", \
    meantable."/prototype2x1mem-*" using 1:($2/$1):($3/$1) with lines title "prototype (two commits)", \
    meantable."/gitallowrepoerr-*" using 1:($4/$1):($5/$1) with lines title "git (garbage collected)" linetype 6, \
