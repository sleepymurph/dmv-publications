set xlabel "File Size (log scale)"
set logscale x 2
set xrange [2**10:2**40]
set format x "%1.0b%BB"

set ylabel "Time to commit"
set logscale y 60
set ytics ( \
    "1ms" .001, "10ms" .01, "20ms" .02, "50ms" .05, \
    "100ms" .1, "200ms" .2, "500ms" .5, \
    "1s" 1, "2s" 2, "5s" 5, "10s" 10, "30s" 30, \
    "1m" 60, "2m" 2*60, "5m" 5*60, "10m" 10*60, "30m" 30*60, \
    "1h" 60*60, "2h" 2*60*60, "5h" 5*60*60, "10h" 10*60*60)

set grid
set key top left

# < grep "False"  selects only the lines with no errors

plot \
    '< ../data/meantable.py --columns filebytes c1_time --files ../data/exp--file-size--v04--w-cpu/git-*' \
        using 1:2:3 with errorlines title "git", \
    '< ../data/meantable.py --columns filebytes c1_time --files ../data/exp--file-size--v04--w-cpu/hg-*' \
        using 1:2:3 with errorlines title "hg", \
    '< ../data/meantable.py --columns filebytes c1_time --files ../data/exp--file-size--v04--w-cpu/bup-*' \
        using 1:2:3 with errorlines title "bup", \
    '< ../data/meantable.py --columns filebytes c1_time --files ../data/exp--file-size--v04--w-cpu/prototype2x1mem-*' \
        using 1:2:3 with errorlines title "prototype", \
    '< ../data/meantable.py --columns filebytes c1_time --files ../data/exp--file-size--v04--w-cpu/copy-*' \
        using 1:2:3 with errorlines title "cp", \
