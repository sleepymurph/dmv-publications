# Use only one set of results, to get a less cluttered graph
high_times_table(p) = "< \
    awk '$7==\"ok\" && $8 >= 0.001 {print $0}' \
    ../data/exp--filesystem-limits--micro/".p."--2017-03-22-murphytest04.txt"

inodes=25688

#set logscale x
#set logscale y

set xrange[1:27500]
set yrange[1:27500]

set xlabel "Total number of files"
set ylabel "Total number of directories"
set zlabel "Write time"

#set logscale cb
#set cblabel "Depth"

set format z "%1.1f s"
set format cb "%g "

set cbrange [0:9]
set palette defined ( \
    0 "green", \
    1 "yellow", \
    2 "orange", \
    3 "red", \
    4 "violet", \
    5 "dark-violet", \
    9 "black" \
)

set key off

set style data impulses

set view 40, 345
set ticslevel 0

set xlabel rotate parallel offset character 0,-2,0
set ylabel rotate parallel offset character -4,-1,0
set zlabel rotate parallel
#set cblabel rotate parallel

set grid
set key at screen .98,.9

unset colorbox

splot \
    for [depth=9:0:-1] \
        high_times_table("4096x*x".sprintf("%02d",depth)) using 4:5:8 \
        lt palette cb depth \
        title "depth=".sprintf("%d",depth), \

    #inodes-x with lines title "total inodes on disk"
