kilos(n) = n * 2**10
megs(n) = n * 2**20
gigs(n) = n * 2**30

set logscale x 2
set xrange [megs(1):gigs(96)]
set format x "%1.0b%BB"
set xtics (megs(1), megs(96), gigs(1), gigs(96))

set yrange [0:100]
set format y "%1.0f%%"

set xtics out
set ytics out

set grid front
unset key

set multiplot layout 3,2 rowsfirst
meantable="< ../data/meantable.py --columns filebytes c1_user c1_system c1_idle c1_iowait --where-not-bad c1_ver --files ../data/exp--file-size--v04--w-cpu"
POS="left at graph 0.05,0.88 front"
USERPLOT='using 1:(($2+$4+$8)/($2+$4+$6+$8)*100) with filledcurves y1=0 title "user"'
SYSTEMPLOT='using 1:(($4+$8)/($2+$4+$6+$8)*100) with filledcurves y1=0 title "system"'
IOWAITPLOT='using 1:(($8)/($2+$4+$6+$8)*100) with filledcurves y1=0 title "iowait"'

set label 1 'git' @POS
gittable=meantable."/git-*"
plot gittable @USERPLOT, gittable @SYSTEMPLOT, gittable @IOWAITPLOT

set label 1 'hg' @POS
hgtable=meantable."/hg-*"
plot hgtable @USERPLOT, hgtable @SYSTEMPLOT, hgtable @IOWAITPLOT

set label 1 'bup' @POS
buptable=meantable."/bup-*"
plot buptable @USERPLOT, buptable @SYSTEMPLOT, buptable @IOWAITPLOT

set label 1 'prototype' @POS
prototypetable=meantable."/prototype2x1mem-*"
plot prototypetable @USERPLOT, prototypetable @SYSTEMPLOT, prototypetable @IOWAITPLOT

set label 1 'copy' @POS
copytable=meantable."/copy-*"
plot copytable @USERPLOT, copytable @SYSTEMPLOT, copytable @IOWAITPLOT

# Separate key: do another plot with outlandish ranges and all decoration off
unset label 1
unset xlabel
unset ylabel
unset xtics
unset ytics
unset grid
unset border
set key center center
set xrange[gigs(256):gigs(1024)]
set yrange[-200:-100]
copytable=meantable."/copy-*"
plot copytable @USERPLOT, copytable @SYSTEMPLOT, copytable @IOWAITPLOT
