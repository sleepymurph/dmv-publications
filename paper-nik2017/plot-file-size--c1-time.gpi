
meantable(f) = "< ../data/meantable.py \
    --columns filebytes c1_time \
    --where-not-bad c1_ver \
    --files ../data/exp--file-size--v04--w-cpu/".f.'-*'

set xlabel "File size"
set format x "%1.0b%BB"

set ylabel "Time to commit"

set grid

kilos(n) = n * 2**10
megs(n) = n * 2**20
gigs(n) = n * 2**30

mins(n) = n * 60

vcslist = "gitallowrepoerr hg bup prototype2x1mem copy"
vcstitles = "Git Mercurial Bup DMV Copy"
vcsstyles = "7 6 5 4 8"

set xrange [gigs(7.5):gigs(104.5)]
set xtics gigs(16)
set yrange [0:330]
set ytics 60
set format y "%1.0fm"
set key

#set title "(e) Largest scales measured"
plot for [i = 1:words(vcslist)] \
    meantable(word(vcslist,i)) using 1:($2/60):($3/60) \
    with errorlines \
    linestyle word(vcsstyles,i) \
    title word(vcstitles,i)
