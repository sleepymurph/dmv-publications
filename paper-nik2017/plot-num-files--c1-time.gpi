
meantable(f)="< ../data/meantable.py \
    --columns filecount c1_time \
    --where-not-bad c1_ver \
    --files ../data/exp--num-files--v03--less-dirs/".f.'-*'

set xlabel "Number of files"
set format x "%1.0s%c"

set ylabel "Time to commit"

set grid

kilos(n) = n * 2**10
megs(n) = n * 2**20
gigs(n) = n * 2**30

mins(n) = n * 60

vcslist = "git hg bup prototype2x1mem copy"
vcstitles = "Git Mercurial Bup DMV Copy"
vcsstyles = "7 6 5 4 8"

set xrange [50000:10500000]
set xtics 1000000
set yrange [0:330]
set ytics 60
set format y "%1.0fm"
set key

#set title "(e) Millions of Files"
plot for [i = 1:words(vcslist)] \
    meantable(word(vcslist,i)) using 1:($2/60):($3/60) \
    with errorlines \
    linestyle word(vcsstyles,i) \
    title word(vcstitles,i)
